// Copyright 2001-2018 Crytek GmbH / Crytek Group. All rights reserved.

#include "Common.cfi"
#include "ShadowCommon.cfi"

float Script : STANDARDSGLOBAL
<
  string Script =           
           "Public;"    
         #if !%THIN_HAIR
					 "ForceZpass;"
				 #else
				   "DepthFixup;"
         #endif
					 "AlphaBlendShadows;"
           "Hair;"
           "NoPreview;"           
           "SupportsAttrInstancing;"
					#if %WIND_BENDING
					 "VT_WindBending;"
					#endif
           "ShaderDrawType = Light;"
           "ShaderType = General;"
           "Cull = NONE;"  
>;

// enable shadow receiving for thin hair
#define THIN_HAIR_SHADOWS	

// calculate shadowing in vertex shader (much faster, but works only on relatively small quads)
// disabled for now as quad size is just too large
// #define THIN_HAIR_VS_SHADOWS

//#define THIN_HAIR_THRESHOLD (0.05)

////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////// Custom shading pass structure /////////////////////////////////////

struct CustomForwardShadingAttribs
{
	bool      bOpaquePass;
	float3    vertexNormal;
	float3    tangent;
	float3    tangentSec;
	float4    dirOcc;
};

#include "ShadeLib.cfi"
#include "ForwardShading.cfi"

//////////////////////////////// Common vertex shader ////////////////

#include "VertexLib.cfi"

//////////////////////////////// Samplers ////////////////

SUBSURFACEMAP
DECALMAP
OPACITYMAP
ENVIRONMENTCUBEMAP
DISSOLVE_NOISE_MAP

sampler2D avgLumSampler = sampler_state
{
 Texture = $HDR_AverageLuminance;
 MinFilter = LINEAR;
 MagFilter = LINEAR;
 MipFilter = LINEAR;
 AddressU = Clamp;
 AddressV = Clamp;
};

Texture2DArray<float4> sunShadowMask = TS_ShadowMask;

Texture2D DirectionTex : register(t4)
<
  UIName = "Direction Map";
  UIDescription = "Tangent space map for defining hair direction";
> = TM_Detail;

half4 AmbientOpPB  : PB_AmbientOpacity;

// Tweakables /////////////////

float SoftIntersection
<
	register = REG_PM_PARAM_0.x;
	string UIName = "Soft Intersection";
	string UIHelp = "Reduce alpha where hair is close to other geometry";
	string UIWidget = "slider";
	float UIMin = 0.0;
	float UIMax = 2.0;
	float UIStep = 0.01;
> = 0.0;

float AlphaBlendMultiplier
<
	register = REG_PM_PARAM_0.y;
	string UIName = "Alpha Blend Multiplier";
	string UIHelp = "Multiplier for transparency";
	string UIWidget = "slider";
	float UIMin = 1.0;
	float UIMax = 4.0;
	float UIStep = 0.1;
> = 1.0;

float SecondaryHighlightWidth
<
	register = REG_PM_PARAM_0.z;
	string UIHelp = "Set width of the secondary highlight";                   
	string UIName = "Secondary Width";    
	string UIWidget = "slider";
	float UIMin = 1;
	float UIMax = 4;
	float UIStep = 0.01;  
> = 1.5;

float4 SecondaryHighlightShiftAmount
<
	register = REG_PM_PARAM_0.w;
	string UIHelp = "Set shift of the secondary highlight";                   
	string UIName = "Secondary Shift";    
	string UIWidget = "slider";
	float UIMin = -0.5;
	float UIMax = 0.5;
	float UIStep = 0.01;  
> = 0.1;

float4 SecondaryHighlightColor
<
	register = REG_PM_PARAM_1;
	string UIHelp = "Set filter color for secondary highlight";                   
	string UIName = "Secondary Color"; 
	string UIGroup = "Highlights";
	string UIOrder = "4";      

	string UIWidget = "color";
> = {0.7, 0.7, 0.7, 1.0};

half ShiftVariation
<  
  register = REG_PM_PARAM_2.x;
  string UIHelp = "Set highlight shifting variation based on the direction map";                     
  string UIName = "Shift Variation";    
  string UIGroup = "Highlights.Shift";
  string UIOrder = "2";          

  string UIWidget = "slider";
  float UIMin = 0;
  float UIMax = 4.0;
  float UIStep = 0.001;
> = 0.0;

half DiffuseWrap
<
  register = REG_PM_PARAM_2.y;
  string UIHelp = "Set diffuse wrapping to reduce harsh lighting";                   
  string UIName = "Diffuse Wrap";    
  
  string UIWidget = "slider";
  float UIMin = 0.0;
  float UIMax = 1;
  float UIStep = 0.001;
> = 0.5;

#if %VIEW_ALIGNED_STRANDS
	float StrandWidth
	<
		register = REG_PM_PARAM_2.z;
		string UIName = "Strand Width";
		string UIWidget = "slider";
		float UIMin = 0.0;
		float UIMax = 0.04;
		float UIStep = 0.001;
	> = 0.01;
#endif

#if %THIN_HAIR
	float ThinHairThreshold
	<
	  register = REG_PM_PARAM_2.w;
	  string UIName = "Thin Hair Threshold";  
	  string UIWidget = "slider";
	  float UIMin = 0.0;
	  float UIMax = 1.0;
	  float UIStep = 0.001;
	> = 0.05;
#endif

struct vert2FragHair
{
	vert2FragGeneral Common;

#if %THIN_HAIR && THIN_HAIR_SHADOWS && THIN_HAIR_VS_SHADOWS
	float SunShadow : TEXCOORDN;
#endif
};

float3 GetViewAlignmentDelta(streamPos vertPassPos, float3 worldTangentT)
{
	float3 expansionDir = normalize( cross( worldTangentT, vertPassPos.WorldPos.xyz - g_VS_WorldViewPos.xyz ) );
	return expansionDir * ((vertPassPos.baseTC.x - 0.5) * StrandWidth);
}

////////////////////////////////////////////////////////
//                GENERAL PASS
////////////////////////////////////////////////////////

vert2FragHair HairVS(app2vertGeneral IN)
{
  vert2FragHair OUT = (vert2FragHair)0;
  
	streamPos vertPassPos = (streamPos)0;
  vs_shared_output( IN, OUT.Common, vertPassPos, false );
    
  float3 worldTangentS = OUT.Common.vTangent.xyz;
  float3 worldTangentT = OUT.Common.vBinormal.xyz;
  float3 worldTangentN = normalize(cross(worldTangentS, worldTangentT)) * OUT.Common.vTangent.w;
	
#if %VIEW_ALIGNED_STRANDS
	vertPassPos.WorldPos.xyz += GetViewAlignmentDelta(vertPassPos, worldTangentT);
	OUT.Common.HPosition = mul( g_VS_ViewProjZeroMatr, float4( vertPassPos.WorldPos.xyz - g_VS_WorldViewPos.xyz, 1) );
#endif
    
  // Support for double sided lighting    
  float3 vEye = normalize( -OUT.Common.vView.xyz );        
  float eyeTangentDot = dot(vEye.xyz, worldTangentN.xyz)*0.8 + 0.2;
  OUT.Common.vTangent.w *= eyeTangentDot;// * OUT.vTangent.w;
 
  OUT.Common.vBinormal.w = 1.0 / 0.5;// AlphaTest.w;

#if %THIN_HAIR && THIN_HAIR_SHADOWS && THIN_HAIR_VS_SHADOWS
	OUT.SunShadow = 1 - Fwd_SampleSunShadowMaps(vertPassPos.WorldPos);
#endif

  return OUT;
}

////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////

void Callback_ShadingDirect(in ForwardShadingAttribs attribs, in ForwardLightParams light, out float3 diffuseAcc, out float3 specularAcc)
{
	half3 vHalf = normalize(-normalize(attribs.worldPos) + light.lightVec);
                          
  half NdotL = dot(attribs.normal, light.lightVec);
	half NdotLVertex = dot(attribs.custom.vertexNormal, light.lightVec);
	
	// Wrapped diffuse, using normalization factor of (1 + DiffuseWrap) to be energy-conserving
	half wrapDenom = 1 + DiffuseWrap;
	half fNdotLDiffuse = saturate(((NdotL + NdotLVertex) * 0.5 + DiffuseWrap) / (wrapDenom * wrapDenom));

  // Diffuse term - wrapped diffuse
  half3 cDiffuse = fNdotLDiffuse;
	
	half3 specCol = min(attribs.reflectance, 0.045);
	
	// Primary highlight (specular surface reflection) 
  half3 cSpecular = KajiyaKayAnisotropic(attribs.custom.tangent, vHalf, exp2(10.0 * attribs.smoothness + 1.0)) * specCol;

  // Secondary highlight (reflected back after entering underneath surface)   
	cSpecular.xyz += KajiyaKayAnisotropic(attribs.custom.tangentSec.xyz, vHalf.xyz, exp2(10.0 * max(attribs.smoothness / SecondaryHighlightWidth, 0.4) + 1.0)) * SecondaryHighlightColor.xyz * attribs.albedo;
	cSpecular.xyz *= saturate(NdotL);
		
	if (attribs.custom.bOpaquePass)
	{
		half fOcclusion = max( 1.0 - saturate( dot( float4( light.lightVec, 0 ), attribs.custom.dirOcc ) ), 0.3 );
		cDiffuse.xyz *= fOcclusion;
		cSpecular.xyz *= fOcclusion;
	}

  diffuseAcc.xyz = cDiffuse.xyz * light.illuminance;
  specularAcc.xyz = cSpecular.xyz * light.illuminance;
}

#include "TiledShading.cfi"

////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////

struct pixoutHair
{
  half4 Color  : SV_Target0;
};

struct pixoutHair2
{
  half4 Color  : SV_Target0;
#if %THIN_HAIR
  half4 BlendFactors : SV_Target1;
#endif
};

float4 HairShading(vert2FragHair IN, bool bOpaquePass)
{
	half4 diffuseMap =  GetDiffuseTex(diffuseTex, IN.Common.baseTC.xy);
	
	half fAlpha = saturate(diffuseMap.w * AlphaBlendMultiplier);
#if !%THIN_HAIR
	if (!bOpaquePass)
		clip(fAlpha - 0.05);
#endif
	
	half3 vNormal = cross(IN.Common.vTangent.xyz, IN.Common.vBinormal.xyz) * IN.Common.vTangent.w;
	half3x3 matTangentToWS = float3x3(IN.Common.vTangent.xyz, IN.Common.vBinormal.xyz, vNormal);

	ForwardShadingAttribs attribs;
	attribs.screenUV = IN.Common.screenProj.xy / IN.Common.screenProj.w;
	attribs.worldPos = IN.Common.vView.xyz;
	attribs.custom.bOpaquePass = bOpaquePass;
	attribs.custom.vertexNormal = vNormal.xyz;
	attribs.custom.dirOcc = 0;

	attribs.albedo = diffuseMap.rgb * MatDifColor.xyz;
#if %VERTCOLORS
	attribs.albedo *= IN.Common.Color.rgb;  // Vertex alpha is reserved for wind bending
#endif
	attribs.reflectance = GetSpecularTex(specularTex, IN.Common.baseTC.xy) * MatSpecColor.xyz;
	attribs.smoothness = clamp(MatSpecColor.w, 0.5, 0.9);

	half3 normalMap = GetNormalMap(normalsTex, IN.Common.baseTC.xy);
	attribs.normal = normalize(mul(normalMap.xyz, matTangentToWS));
	attribs.normalSpecProbe = attribs.normal;
	
	half shiftAmount = 0;
	half3 hairDir = IN.Common.vBinormal.xyz;
#if %DIRECTION_MAP
	half3 vDirection = GetNormalMap(DirectionTex, IN.Common.baseTC.xy);
	hairDir = mul(vDirection.xyz, matTangentToWS);
	shiftAmount = vDirection.z * ShiftVariation;
#endif

	const half PrimarySpecShift = 0;
	attribs.custom.tangent = ShiftVector(hairDir.xyz, attribs.normal.xyz * IN.Common.vTangent.w, shiftAmount + PrimarySpecShift);
	attribs.custom.tangentSec = ShiftVector(hairDir.xyz, attribs.normal.xyz * IN.Common.vTangent.w, shiftAmount + SecondaryHighlightShiftAmount);
	
	// Soft intersection
	if (SoftIntersection > 0)
	{
		float2 tcProj = (IN.Common.screenProj.xy / IN.Common.screenProj.w);
		int3 vPixCoord = int3(max(min(tcProj * CV_ScreenSize.xy, CV_ScreenSize.xy - float2(1.0f, 1.0f)), float2(0.0f, 0.0f)), 0);
		float fDepth = Fwd_SceneDepth.Load(vPixCoord).x * CV_NearFarClipDist.y;
		float depthDiff = abs(IN.Common.screenProj.w - fDepth);
		fAlpha *= saturate(lerp(1, saturate(depthDiff * 100), SoftIntersection));
	}
	
	half3 diffuseAcc = 0;
	half3 specularAcc = 0;
	
	// Handle direct lighting from sun
	{
		half shadowOccl = 0;

	#if %THIN_HAIR && THIN_HAIR_SHADOWS
		#if THIN_HAIR_VS_SHADOWS
			if (!bOpaquePass)
				shadowOccl = IN.SunShadow;
		#else
			shadowOccl = Fwd_SampleSunShadowMaps(IN.Common.vView.xyz + GetWorldViewPos());
		#endif
	#else
		if (bOpaquePass)
		{
			int4 vPixCoord = int4(attribs.screenUV * CV_ScreenSize.xy, 0, 0);
			shadowOccl = Fwd_SunShadowMask.Load(vPixCoord).r;
		}
		else
		{
			shadowOccl = Fwd_SampleSunShadowMaps(IN.Common.vView.xyz + GetWorldViewPos());
		}
	#endif

		ForwardLightParams lightParams;
		lightParams.type = LT_DIRECTIONAL;
		lightParams.lightVec = CV_SunLightDir.xyz;
		lightParams.illuminance = CV_SunColor.xyz * (1 - shadowOccl);
		
		Callback_ShadingDirect(attribs, lightParams, diffuseAcc, specularAcc);
	}

#if %_RT_TILED_SHADING
	const bool highQualityShadowFiltering = false;

	#if %_RT_QUALITY1 || %ENFORCE_TILED_SHADING
		const bool applyLights = true;
	#else
		const bool applyLights = false;
	#endif
	
	TiledForwardShading(attribs, diffuseAcc, specularAcc, ssFwdBilinearWrap, ssFwdComparison, highQualityShadowFiltering, true, applyLights, true);
#endif

#if !%_RT_TILED_SHADING
	#if FEATURE_SVO_GI
		ApplyGI(specularAcc, diffuseAcc, diffuseAcc, attribs.screenUV + .5f / CV_ScreenSize.xy, attribs.smoothness, attribs.reflectance, 
			Fwd_GiDiffuseRT, Fwd_GiSpecularRT, TrilinearClampSamplerState, (int)(ForwGiIntegrationMode.y));
	#endif
#endif
	
	// Final composition
	half4 cFinal = half4(diffuseAcc * attribs.albedo.rgb + specularAcc, fAlpha);

	Fwd_ApplyFog(IN.Common.vView.xyz + GetWorldViewPos(), IN.Common.vView.xyz, IN.Common.WPos, cFinal.xyz);

	return cFinal;
}

pixoutHair HairOpaquePS(vert2FragHair IN)
{
	pixoutHair OUT = (pixoutHair) 0;  
  OUT.Color = HairShading(IN, true);
  return OUT;  
}

pixoutHair2 HairPS(vert2FragHair IN)
{
	pixoutHair2 OUT = (pixoutHair2) 0;
  
	OUT.Color = HairShading(IN, false);
	
#if %THIN_HAIR
	OUT.BlendFactors.a = OUT.Color.a;
	float fZ = IN.Common.WPos.w * CV_NearFarClipDist.w;
	OUT.Color.a = (OUT.Color.a > ThinHairThreshold) ? -fZ : 1;
#endif
	
  return OUT;
}

pixoutHair2 ThinHairDepthPS(vert2FragHair IN)
{
	pixoutHair2 OUT = (pixoutHair2)0;
	
	float fOpacity = saturate(GetAlphaTex(diffuseTex, IN.Common.baseTC) * AlphaBlendMultiplier) * AmbientOpPB.a;
	clip(fOpacity - ThinHairThreshold);
	
	return OUT;
}

//////////////////////////////// technique ////////////////


technique General
<
  string Script =     
        "TechniqueZ=ZPass;"      
        "TechniqueMotionBlur=MotionBlurHair;"
        "TechniqueCustomRender=CustomRenderHair;"
        "TechniqueShadowGen=ShadowGenHair;"
>
{
	
#if !%THIN_HAIR
	pass opaque_pass
  {
    VertexShader = HairVS() HairVS;
    PixelShader = HairOpaquePS() HairPS;
    
    ZEnable = true;
    ZWriteEnable = true;
    //ZFunc = Equal;

    CullMode = None;
   // IgnoreMaterialState = true;
  }

	pass front_pass
  {
    VertexShader = HairVS() HairVS;
    PixelShader = HairPS() HairPS;
    
    ZEnable = true;
    ZWriteEnable = false;
    ZFunc = Less;
    CullMode = Front;
    IgnoreMaterialState = true;
  }
#endif

  pass back_pass
  {
    VertexShader = HairVS() HairVS;
    PixelShader = HairPS() HairPS;
    
    ZEnable = true;
    ZWriteEnable = false; 
    ZFunc = Less;
    CullMode = Back;
    IgnoreMaterialState = true;
  }

#if %THIN_HAIR
	pass depth_pass
  {
    VertexShader = HairVS() HairVS;
    PixelShader = ThinHairDepthPS() HairPS;
    
    ZEnable = true;
    ZWriteEnable = true;
    ZFunc = Less;
    CullMode = Back;
    IgnoreMaterialState = true;
		ColorWriteEnable = 0;
  }
#endif
}


//////////////////////////////// Common techniques ////////////////

//#include "CommonZPass.cfi"

//////////////////////////////////////////////////////////////////
// Custom Zpass for Hair:
//	- avoid poluting even further common zpass with yet aditional permutation

///////////////// vertex input/output //////////////////

struct vert2fragZ_C
{
  OUT_HPOS_IN_WPOS

	float4 baseTC    : TEXCOORDN;
	half4 vTangent   : TEXCOORDN;	
	half4 vBinormal  : TEXCOORDN;

#if %_RT_MOTION_BLUR && !%_RT_ALPHABLEND
	float3 HPosPrev : TEXCOORDN;
#endif
};

///////////////// vertex shaders //////////////////
vert2fragZ_C ZPassVS(app2vertGBuffer IN)
{
  vert2fragZ_C OUT = (vert2fragZ_C)0;
  streamPos vertPassPos = (streamPos)0;
  streamPos_FromGBuffer(IN, vertPassPos);
	streamPos vertPassPosPrev = (streamPos) vertPassPos;

  OUT.HPosition = Pos_VS_General(g_VS_ViewProjZeroMatr, vertPassPos);  
#if %_RT_MOTION_BLUR && !%_RT_ALPHABLEND
	vertPassPosPrev.PositionCurr = vertPassPos.Position;
	OUT.HPosPrev.xyz = HPosToScreenTC( Pos_Prev_VS_General(g_VS_ViewProjZeroMatr, vertPassPosPrev) ).xyw;
#endif

  float3 vPos = mul(vertPassPos.InstMatrix, vertPassPos.Position).xyz;
  #if !%_RT_OBJ_IDENTITY
    vPos += g_VS_WorldViewPos.xyz;
  #endif
	
	// Note: Normalization required for normal diffuse map in world space in case scale used - Do not remove
  float3 worldTangentS = normalize( mul((const float3x3)vertPassPos.InstMatrix, vertPassPos.ObjToTangentSpace[0]) );
  float3 worldTangentT = normalize( mul((const float3x3)vertPassPos.InstMatrix, vertPassPos.ObjToTangentSpace[1]) );
  float3 worldTangentN = normalize( mul((const float3x3)vertPassPos.InstMatrix, vertPassPos.ObjToTangentSpace[2]) );
	
#if %VIEW_ALIGNED_STRANDS	
	// Use exactly the same math as for the the General pass, otherwise there will be precision issues
	vertPassPos.WorldPos.xyz += g_VS_WorldViewPos.xyz;
	vertPassPos.WorldPos.xyz += GetViewAlignmentDelta(vertPassPos, worldTangentT);
	OUT.HPosition = mul( g_VS_ViewProjZeroMatr, float4( vertPassPos.WorldPos.xyz - g_VS_WorldViewPos.xyz, 1) );
	vertPassPos.WorldPos.xyz -= g_VS_WorldViewPos.xyz;
#endif

	////////////////////////////////////////////////////////////////////////////////////////////////
  // Output tangents

  float3 viewVector = normalize(-vertPassPos.WorldPos.xyz );
  float fFlipNormal = min( dot(viewVector, worldTangentN) +0.1,1.f);
  vertPassPos.Tangent.w *= fFlipNormal;

  OUT.vTangent = float4(worldTangentS, vertPassPos.Tangent.w); 
  OUT.vBinormal.xyz = worldTangentT;

	////////////////////////////////////////////////////////////////////////////////////////////////
  // Output uv coordinates
 
  float4 baseTC = vertPassPos.baseTC;  
  
  _TCModify(baseTC, OUT.baseTC, vertPassPos.Position, vertPassPos.ObjToTangentSpace[2], TS_DIFFUSE);	

  #if %_RT_ALPHATEST || %_RT_DISSOLVE
	OUT.baseTC.wz = float2(Get_MaterialAlphaTest(),Get_SPI_Dissolve());
  #endif
  
  return OUT;            
}

///////////////// pixel shaders //////////////////

pixout_gbuffer ZPassPS(vert2fragZ_C IN)
{
  pixout_gbuffer OUT = (pixout_gbuffer) 0;
	
	////////////////////////////////////////////////////////////////////////////////////////////////
  // Depth and alpha test processing

#if %_RT_ALPHATEST	
	half fDiffuseAlpha = GetAlphaTex(diffuseTex, IN.baseTC.xy);
	#if %_RT_MSAA_QUALITY || %_RT_MSAA_QUALITY1
		GetAlphaCoverageMask(diffuseTex, IN.baseTC.xy, IN.baseTC.w, fDiffuseAlpha, OUT.uCoverageMask);
	#endif
	clip(fDiffuseAlpha - IN.baseTC.w);
#endif

//#if %_RT_DISSOLVE
//	const int2 vPixQuadIdx = (int2)IN.WPos.xy % 2;
//	clip(IN.vBinormal.w * (abs(dot(half4(0.0h, 0.76h, 0.5h,-1.0h), vPixQuadIdx.xxyy)) - IN.baseTC.z));
//#endif

  ////////////////////////////////////////////////////////////////////////////////////////////////
  // Normals processing

  MaterialAttribsCommon attribs = MaterialAttribsDefault();
	
	half4 cOutNormal = 0;
  half3 vNormalTS = half3(0,0,1);

  half3x3 mTangentToWS = half3x3(IN.vTangent.xyz, IN.vBinormal.xyz, cross(IN.vTangent.xyz, IN.vBinormal.xyz)*IN.vTangent.w);  
	half3 vNormal = normalize(mTangentToWS[2]);
	#if %NORMAL_MAP
		vNormalTS = GetNormalMap(normalsTex, IN.baseTC.xy);
		vNormal = normalize(mul( vNormalTS, mTangentToWS )); 
	#endif
	
	attribs.NormalWorld = vNormal;
	attribs.Smoothness = MatSpecColor.w;
	
	////////////////////////////////////////////////////////////////////////////////////////////////
	// Output to GBuffer
	
	attribs.Albedo = half3(0.5, 0.5, 0.5);
	
#if %_RT_SAMPLE0
	EncodeMicroGBuffer(attribs, OUT.GBufferA, IN.WPos.xy);
#else		
	EncodeGBuffer( attribs, OUT.GBufferA, OUT.GBufferB, OUT.GBufferC );
#endif

#if %_RT_MOTION_BLUR && !%_RT_ALPHABLEND
	OUT.VelocityRT = OutputVelocityRT(IN.WPos.xy * (CV_ScreenSize.zw * 2), IN.HPosPrev.xy/IN.HPosPrev.z);
#endif

	return OUT;
}

//////////////////////////////// technique ////////////////

technique ZPass
{
  pass p0
  {
    VertexShader = ZPassVS() ZVS;
    PixelShader = ZPassPS() ZPS;

    ZEnable = true;
    ZWriteEnable = true;  

    CullMode = None;

  }
}

///////////////// Shadow gen //////////////////////
#include "CommonShadowGenPass.cfi"

v2f_sh HairSG_VS(app2vertGeneral IN)
{
	v2f_sh OUT = (v2f_sh)0; 
	streamPos vertPassPos = (streamPos)0;
	vert2FragGeneral common = (vert2FragGeneral)0;
	
	vs_shared_output( IN, common, vertPassPos, false, false );
  
	float3 worldTangentS = common.vTangent.xyz;
	float3 worldTangentT = common.vBinormal.xyz;
	float3 worldTangentN = (cross(worldTangentS, worldTangentT)) * common.vTangent.w;

	vertPassPos.WorldPos = mul( vertPassPos.InstMatrix, vertPassPos.Position );
	
#if %VIEW_ALIGNED_STRANDS
	vertPassPos.WorldPos.xyz += GetViewAlignmentDelta(vertPassPos, worldTangentT);
#endif

	float4 HPosition = mul(g_VS_ViewProjMatr, vertPassPos.WorldPos);

#if !%_RT_CUBEMAP0 
	#if !%_RT_NEAREST
		HPosition.z /= CP_ShadowGen_FrustumInfo.y;
	#endif

	#if %_RT_SAMPLE0
		OUT.Depth = HPosition.z + CP_ShadowGen_DepthTestBias.x;
	#endif
	
	#if !%_RT_NEAREST
		HPosition.z *= HPosition.w;
	#endif
#else 
	OUT.Depth = (HPosition.z/CP_ShadowGen_FrustumInfo.y);
	OUT.Depth += CP_ShadowGen_DepthTestBias.x;
#endif

#if %_RT_ALPHATEST || %_RT_ALPHABLEND || %_RT_DISSOLVE
	OUT.baseTC.xy = vertPassPos.baseTC.xy;
#endif

#if %_DS || %_PS || !ENABLE_TESSELLATION || %_RT_NO_TESSELLATION
	OUT.HPosition = HPosition;
#endif  

  return OUT;
}

technique ShadowGenHair
{
	pass p0
	{
		VertexShader = HairSG_VS() ShadowGenVS;

		ZEnable = true;
		ZWriteEnable = true;
		CullMode = Back;

		PixelShader = Common_SG_PS() ShadowGenPS;

#if %GRASS || %LEAVES || %HAIR_PASS || %SKIN_PASS
		CullMode = None;
#endif
	}
}

#include "CommonMotionBlurPass.cfi"

vert2fragMotionBlur Hair_MotionBlurPassVS(app2vertMotionBlur IN)
{
	vert2fragMotionBlur OUT = (vert2fragMotionBlur)0;

	streamPos vertPassPos = (streamPos)0;
	streamPos_FromMotionBlur(IN, vertPassPos);
	streamPos vertPassPosPrev = (streamPos) vertPassPos;

	float4 HPosition = 0;

	HPosition = Pos_VS_General(g_VS_ViewProjZeroMatr, vertPassPos);
	OUT.vVelocity = HPosToScreenTC( HPosition );
#if %VIEW_ALIGNED_STRANDS
		float3 worldTangentT = normalize( mul((const float3x3)vertPassPos.InstMatrix, vertPassPos.ObjToTangentSpace[1]) );
		vertPassPos.WorldPos.xyz += g_VS_WorldViewPos.xyz;
		vertPassPos.WorldPos.xyz += GetViewAlignmentDelta(vertPassPos, worldTangentT);
		HPosition = mul( g_VS_ViewProjZeroMatr, float4( vertPassPos.WorldPos.xyz - g_VS_WorldViewPos.xyz, 1) );
		vertPassPos.WorldPos.xyz -= g_VS_WorldViewPos.xyz;
#endif
	vertPassPosPrev.PositionCurr = vertPassPos.Position;

	float4x4 matInstPrev;
	float4 HPositionPrev = Pos_Prev_VS_General(g_VS_ViewProjZeroMatr, vertPassPosPrev);
	OUT.vVelocityPrev = HPosToScreenTC( HPositionPrev );

	OUT.HPosition = HPosition;

	float4 baseTC = vertPassPos.baseTC;
	_TCModify(baseTC, OUT.baseTC, vertPassPos.Position, vertPassPos.ObjToTangentSpace[2], TS_DIFFUSE);

	OUT.baseTC.w = 0;
	OUT.baseTC.z = 0;

	return OUT;
}

pixout Hair_MotionBlurPassPS(vert2fragMotionBlur IN)
{
	pixout OUT = (pixout)1;

	float fOpacity = saturate(GetAlphaTex(diffuseTex, IN.baseTC) * AlphaBlendMultiplier) * AmbientOpPB.a;

	#if !%THIN_HAIR
		clip(fOpacity - Get_MaterialAlphaTest());
	#else
		clip(fOpacity - ThinHairThreshold);
	#endif

	half fAlpha = 1;
#if %_RT_ALPHABLEND || %THIN_HAIR
	// In case alpha blended scale values
	fAlpha = fOpacity;
#endif

	OUT.Color = OutputVelocityRT(IN.vVelocity.xy/IN.vVelocity.w, IN.vVelocityPrev.xy/IN.vVelocityPrev.w, fAlpha);

	return OUT;
}

technique MotionBlurHair
{
pass p0
	{
		VertexShader = Hair_MotionBlurPassVS() MotionBlurVS;
		PixelShader = Hair_MotionBlurPassPS() MotionBlurPS;

		ZEnable = true;
		ZWriteEnable = false;
		CullMode = None;

		ZFunc = LEqual;
		IgnoreMaterialState = true;
	}
}

#include "CommonViewsPass.cfi"

///////////////// vertex shaders //////////////////
vert2fragCustomRender Hair_CustomRenderPassVS(app2vertCustomViews IN)
{
	vert2fragCustomRender OUT = (vert2fragCustomRender)0;

	streamPos vertPassPos = (streamPos)0;
	streamPos_FromCustomViews(IN, vertPassPos);

	OUT.cVision = PerInstanceCustomData1;

	OUT.HPosition = Pos_VS_General(g_VS_ViewProjZeroMatr, vertPassPos);
	OUT.vView.xyz = vertPassPos.WorldPos.xyz;

	// reminder for Andrey, texture modifiers should work on all cases
	_TCModify(vertPassPos.baseTC, OUT.baseTC, vertPassPos.Position, vertPassPos.ObjToTangentSpace[2], TS_DIFFUSE);

	OUT.baseTC.z = OUT.HPosition.w;

	float3 worldPos = float3(vertPassPos.InstMatrix[0].w, vertPassPos.InstMatrix[1].w, vertPassPos.InstMatrix[2].w);
	OUT.baseTC.w = frac( length( worldPos.xyz + g_VS_WorldViewPos.xyz) ) + 0.01;

	// Output the screen-space texture coordinates - for shadows and general projections
	OUT.screenProj = HPosToScreenTC(OUT.HPosition);

#if %VIEW_ALIGNED_STRANDS
	float3 worldTangentT = normalize( mul((const float3x3)vertPassPos.InstMatrix, vertPassPos.ObjToTangentSpace[1]) );
#endif

/*
#if %_RT_SAMPLE1
	// Note: Normalization required for normal diffuse map in world space in case scale used - Do not remove
	float3 worldTangentS = normalize( mul((const float3x3)vertPassPos.InstMatrix, vertPassPos.ObjToTangentSpace[0]) );
	float3 worldTangentN = (cross(worldTangentS, worldTangentT)) * vertPassPos.Tangent.w;

	OUT.vTangent = float4(worldTangentS, vertPassPos.Tangent.w); 
	OUT.vBinormal.xyz = worldTangentT;
#endif
*/

#if %VIEW_ALIGNED_STRANDS
	vertPassPos.WorldPos.xyz += g_VS_WorldViewPos.xyz;
	vertPassPos.WorldPos.xyz += GetViewAlignmentDelta(vertPassPos, worldTangentT);
	OUT.HPosition = mul( g_VS_ViewProjZeroMatr, float4( vertPassPos.WorldPos.xyz - g_VS_WorldViewPos.xyz, 1) );
	vertPassPos.WorldPos.xyz -= g_VS_WorldViewPos.xyz;
#endif

	return OUT;
}

technique CustomRenderHair
{
pass p0
	{
		VertexShader = Hair_CustomRenderPassVS() CustomRenderVS;
		PixelShader = Common_CustomRenderPassPS() CustomRenderPS;

		ZEnable = true;
		ZWriteEnable = false;
		CullMode = None;

		ZFunc = LEqual;
		IgnoreMaterialState = true;
	}
}

