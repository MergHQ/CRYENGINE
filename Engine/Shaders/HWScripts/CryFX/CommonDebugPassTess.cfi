// Copyright 2001-2018 Crytek GmbH / Crytek Group. All rights reserved.

#if ENABLE_TESSELLATION

struct HS_CONTROL_POINT_OUTPUT_DEBUGPASS
{
	hsControlPointShared controlPoint;
};

///////////////// hull shader //////////////////

HS_CONSTANT_DATA_OUTPUT DebugPassConstantsHS(InputPatch<vert2fragDebug, 3> p, uint PatchID : SV_PrimitiveID)
{
	const float3 vP0 = p[0].vView.xyz + g_VS_WorldViewPos.xyz;
	const float3 vP1 = p[1].vView.xyz + g_VS_WorldViewPos.xyz;
	const float3 vP2 = p[2].vView.xyz + g_VS_WorldViewPos.xyz;
	HS_CONSTANT_DATA_OUTPUT output = CommonConstantsHS(vP0, vP1, vP2, g_VS_WorldViewPos.xyz, g_VS_WorldViewPos.xyz, PatchID);

	return output;
}

[domain("tri")]
[partitioning("fractional_odd")]
[outputtopology("triangle_cw")]
[outputcontrolpoints(3)]
[patchconstantfunc("DebugPassConstantsHS")]
[maxtessfactor(MAX_TESS_FACTOR)]
HS_CONTROL_POINT_OUTPUT_DEBUGPASS Common_DebugPassHS(InputPatch<vert2fragDebug, 3> inputPatch, uint uCPID : SV_OutputControlPointID)
{
	HS_CONTROL_POINT_OUTPUT_DEBUGPASS	output = (HS_CONTROL_POINT_OUTPUT_DEBUGPASS)0;

	output.controlPoint.vView   = inputPatch[uCPID].vView;
	output.controlPoint.vNormal = inputPatch[uCPID].vNormal;
	output.controlPoint.vBaseTC = inputPatch[uCPID].baseTC;

	return output;
}

///////////////// domain shader //////////////////

void FillEvalInput_DebugPass(in const OutputPatch<HS_CONTROL_POINT_OUTPUT_DEBUGPASS, 3> triPatch, in HS_CONSTANT_DATA_OUTPUT hsConstData, in float3 vBaryCoords, inout CommonEvaluationInputDS evalInput)
{
	evalInput.vBaryCoords = vBaryCoords;
	FillEvalInputControlPoint(triPatch[0].controlPoint, triPatch[1].controlPoint, triPatch[2].controlPoint, hsConstData, evalInput);
	evalInput.bRelativePos = true;
	evalInput.vViewPos = g_VS_WorldViewPos.xyz;
}

void ProcessEvalOutput_DebugPass(in CommonEvaluationOutputDS evalOutput, inout vert2fragDebug dsOutput)
{
	dsOutput.baseTC = evalOutput.vBaseTC;

	dsOutput.HPosition = mul(g_VS_ViewProjZeroMatr, float4(evalOutput.vPos.xyz, 1));
}

[domain("tri")]
vert2fragDebug Common_DebugPassDS( HS_CONSTANT_DATA_OUTPUT hsConstData, float3 vBaryCoords : SV_DomainLocation, const OutputPatch<HS_CONTROL_POINT_OUTPUT_DEBUGPASS, 3> TrianglePatch )
{
	vert2fragDebug output = (vert2fragDebug)0;

	CommonEvaluationInputDS evalInput = (CommonEvaluationInputDS)0;
	FillEvalInput_DebugPass(TrianglePatch, hsConstData, vBaryCoords, evalInput);

	CommonEvaluationOutputDS evalOutput;
	Evaluate(evalInput, evalOutput);
	ProcessEvalOutput_DebugPass(evalOutput, output);

	return output;
}

#endif
