// CRYENGINE Source Code File
// Copyright 2001-2015 Crytek GmbH. All rights reserved.

////////////////////////////////////////////////
// shader constants

float4 GlowParams           : PB_GlowParams;
float4 EnvProbeMult         = {PB_ObjVal[0], PB_ObjVal[1], PB_ObjVal[2], PB_ObjVal[3]};
float4 TexTileSize          : PI_TextureTileSize;     // xy = tile width/height, z = tile count, w = start
half4 ParticleParams        : PI_ParticleParams;      // per emitter parameters: xy = expansion, z = curvature, w = texture frequency
half4 ParticleLightParams   : PI_ParticleLightParams; // x = Diffuse term, y = Emissive term, z = back lighting term
half3 ParticleSoftParams    : PI_ParticleSoftParams;  // x = softness scale, y = spherical param, z = volume thickness
half3 ParticleAlphaTest[2]  : PI_ParticleAlphaTest;   // Coeffs for (AT0 + alpha AT1): x = alpha scale, y = source min, z = source width

float4 PullVerticesInfo : PB_PullVerticesInfo;					// x = vertex buffer offset

#define HALFRES_DEBUG_COLOR half3(0.3h, 3.h, 0.3h)


struct SParticleAxes
{
	float3 XAxis;
	float3 YAxis;
};

struct SParticleColorST
{
	uint Color;
	uint baseTC;
};

Buffer<float3>                     positionsSRV : register(t7);
StructuredBuffer<SParticleAxes>    axesSRV      : register(t8);
StructuredBuffer<SParticleColorST> colorSTsSRV  : register(t9);


struct app2vertParticleGeneral
{
	float4 Color     : COLOR0;
	float4 baseTC    : COLOR1;
	float3 XAxis     : TEXCOORD0;
	float3 YAxis     : TEXCOORD1;
};

//////////////////////////////////////////////////////////////////////////////////////////////////
// Common particle vertex structure for all shared vertex shader processing.
//  - check ParticleThicknessVS for example usage

struct SParticleVertex
{
	float4 HPosition;
	float4 WPos;

	float4 baseTC;      // xy = final tc; zw = orig tc
	float4 blendTC;     // 2nd tex coords, z is blend value for 2nd texture tile; w = shadow occlusion
	half4 alphaClip;    // x = alpha scale, y = source alpha min, z = source alpha range; w = thickness coeff (soft)

	// These 3 interpolators are redundant - todo: pass instead WorldPos and WorldCenterPos, compute normal/tangents per-pixel
	half4 vWorldPos;    // w is binormal.z
	half4 vTangent;     // w is binormal.y
	half4 vNormal;      // w is binormal.x (if lighting and material are used)

	half4 localFogColor;
	
	half4 Color;

	half4 lightData0;
	half4 lightData1;
};

void Particle_Setup_General(inout float4 vPos, float4x4 mViewProj, inout app2vertParticleGeneral IN, inout SParticleVertex OUT)
{
	// ParticleParams.xy: 0 = no expand, -1 = expand position only, 1 = expand position and curved normals.

	// Apply TCs to axes
	const half2 xyVertCoord = IN.baseTC.xy * 2 - 1;
	const half2 xyExpand = xyVertCoord * abs(ParticleParams.xy);
	const half2 xyCurve = xyVertCoord * saturate(ParticleParams.xy);
	
	// Expand positions and curved side normals.
	float3 vToCam = vfViewPos - vPos.xyz;

	vPos.xyz += IN.XAxis * xyExpand.x + IN.YAxis * xyExpand.y;

	float3 vViewNormal = vfViewPos - vPos;
	float3 vNormal = cross(IN.YAxis, IN.XAxis);
	vNormal *= sign(dot(vViewNormal, vNormal));
	float3 vBitangent;

	//////////////////////////////////////////////////////
	if (dot(xyCurve, xyCurve))
	{
		// Camera-facing
		const float fCurveExpand = ParticleSoftParams.y;
		if (fCurveExpand)
		{
			half fXLen = length(IN.XAxis),
			     fYLen = length(IN.YAxis),
			     fMinLen = min(fXLen, fYLen);
			float3 vCurveNormal = IN.XAxis * (xyCurve.x * fMinLen / fXLen) + IN.YAxis * (xyCurve.y * fMinLen / fYLen);
			
			// Move vertices to curved particle tangent point
			float fTanSqr = dot(vCurveNormal, vCurveNormal);
			fTanSqr /= max(fTanSqr, dot(vToCam, vToCam));
			vPos.xyz += (vToCam * fTanSqr + vCurveNormal * (1 - sqrt(1 - fTanSqr))) * fCurveExpand;
		
			// Fade out when close to sphere
			half fFade = lerp(1.0, saturate(2 - fTanSqr * 2.5), fCurveExpand);
			IN.Color.a *= fFade;
		}
						
		if (ParticleParams.z)
		{
			// Curved normals
			// Rotate tangents for fake round geometry

			float3 vSideNormal = IN.XAxis * xyCurve.x + IN.YAxis * xyCurve.y;
			float3 vCurvedNormal = vSideNormal * dot(vViewNormal, vViewNormal) - vViewNormal * dot(vViewNormal, vSideNormal);
			OUT.vNormal.xyz = lerp(vNormal * length(vCurvedNormal), vCurvedNormal * length(vNormal), ParticleParams.z);

			#if USE_MATERIAL
				float3 vAxis = cross(vNormal, OUT.vNormal.xyz);
				float c = dot(vNormal, OUT.vNormal.xyz);
				OUT.vTangent.xyz = IN.XAxis * c + cross(vAxis, IN.XAxis);
				vBitangent = IN.YAxis * c + cross(vAxis, IN.YAxis);
			#endif
		}
	}
	
	if (!ParticleParams.z)
	{
		// Flat sprite normals
		if (dot(vNormal, vToCam) < 0)
			// Implement 2-sided
			vNormal = -vNormal;
		OUT.vNormal.xyz = vNormal;
		#if USE_MATERIAL
			OUT.vTangent.xyz = IN.XAxis;
			vBitangent = IN.YAxis;
		#endif
	}

	OUT.vNormal.xyz = normalize(OUT.vNormal.xyz);

	#if USE_MATERIAL
		// Output bitangent in w components to save 1 interpolator (2 extra alu in pixel shader though)
		OUT.vTangent.xyz = normalize(OUT.vTangent.xyz);
		vBitangent = normalize(vBitangent);
		OUT.vNormal.w = vBitangent.x;
		OUT.vTangent.w = vBitangent.y;
		OUT.vWorldPos.w = vBitangent.z;
	#endif

	OUT.vWorldPos.xyz = vPos.xyz;
	OUT.HPosition = mul(mViewProj, vPos);
}

//========================================================================
